/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'Ruben' at '22/10/15 17:32' with Gradle 2.6
 *
 * This generated file contains a sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/2.6/userguide/tutorial_java_projects.html
 */
 buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.com.stehno:gradle-natives:0.2.3"
    }
}

apply plugin: "com.stehno.natives"
apply plugin: 'java'
apply plugin: 'application'

mainClassName = "com.valarion.gameengine.core.GameCore"
applicationDefaultJvmArgs = ["-DruningInEclipse=true"]

repositories {
    jcenter()
}

dependencies {
    compile 'org.slick2d:slick2d-core:1.0.0'
}

test {
    systemProperty 'java.library.path', file('build/natives')
}

run {
    systemProperty 'java.library.path', file('build/natives')
    workingDir = buildDir
}

natives {
    jars = [
        'lwjgl-platform-2.9.0-natives-windows.jar',
        'jinput-platform-2.0.5-natives-windows.jar',
        'lwjgl-platform-2.9.0-natives-linux.jar',
        'jinput-platform-2.0.5-natives-linux.jar',
        'lwjgl-platform-2.9.0-natives-osx.jar',
        'jinput-platform-2.0.5-natives-osx.jar'
    ]
}

task copyNatives(type: Copy, dependsOn: unpackNatives) {
	    from 'build/natives/windows'
	    from 'build/natives/linux'
	    from 'build/natives/osx'
	    into("build/natives")
	}
	
copyNatives.mustRunAfter(unpackNatives)

run.dependsOn copyNatives

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}